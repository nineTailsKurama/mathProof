/-
Copyright (c) 2022 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import analysis.complex.cauchy_integral
import analysis.convex.integral
import analysis.normed_space.completion
import topology.algebra.order.extr_closure

/-!
# Maximum modulus principle

In this file we prove several versions of the maximum modulus principle.

There are several statements that can be called "the maximum modulus principle" for maps between
normed complex spaces.

In the most general case, see `complex.norm_eventually_eq_of_is_local_max`, we can only say that for
a differentiable function `f : E ‚Üí F`, if the norm has a local maximum at `z`, then *the norm* is
constant in a neighborhood of `z`.

If the domain is a nontrivial finite dimensional space, then this implies the following version of
the maximum modulus principle, see `complex.exists_mem_frontier_is_max_on_norm`. If `f : E ‚Üí F` is
complex differentiable on a nonempty compact set `K`, then there exists a point `z ‚àà frontier K`
such that `Œª z, ‚à•f z‚à•` takes it maximum value on `K` at `z`.

Finally, if the codomain is a strictly convex space, then the function cannot have a local maximum
of the norm unless the function (not only its norm) is a constant. This version is not formalized
yet.
-/

open topological_space metric set filter asymptotics function measure_theory affine_map
open_locale topological_space filter nnreal real

universes u v w
variables {E : Type u} [normed_group E] [normed_space ‚ÑÇ E]
  {F : Type v} [normed_group F] [normed_space ‚ÑÇ F]

local postfix `ÃÇ`:100 := uniform_space.completion

namespace complex

/-!
### Auxiliary lemmas

We split the proof into a series of lemmas. First we prove the principle for a function `f : ‚ÑÇ ‚Üí F`
with an additional assumption that `F` is a complete space, then drop unneeded assumptions one by
one.

The only "public API" lemmas in this section are TODO and
`complex.norm_eq_norm_of_is_max_on_of_closed_ball_subset`.
-/

lemma norm_max_aux‚ÇÅ [complete_space F] {f : ‚ÑÇ ‚Üí F} {z w : ‚ÑÇ}
  (hd : diff_on_int_cont ‚ÑÇ f (closed_ball z (dist w z)))
  (hz : is_max_on (norm ‚àò f) (closed_ball z (dist w z)) z) :
  ‚à•f w‚à• = ‚à•f z‚à• :=
begin
  /- Consider a circle of radius `r = dist w z`. -/
  set r : ‚Ñù := dist w z,
  have hw : w ‚àà closed_ball z r, from mem_closed_ball.2 le_rfl,
  /- Assume the converse. Since `‚à•f w‚à• ‚â§ ‚à•f z‚à•`, we have `‚à•f w‚à• < ‚à•f z‚à•`. -/
  refine (is_max_on_iff.1 hz _ hw).antisymm (not_lt.1 _),
  rintro hw_lt : ‚à•f w‚à• < ‚à•f z‚à•,
  have hr : 0 < r, from dist_pos.2 (ne_of_apply_ne (norm ‚àò f) hw_lt.ne),
  /- Due to Cauchy integral formula, it suffices to prove the following inequality. -/
  suffices : ‚à•‚àÆ Œ∂ in C(z, r), (Œ∂ - z)‚Åª¬π ‚Ä¢ f Œ∂‚à• < 2 * œÄ * ‚à•f z‚à•,
  { refine this.ne _,
    have A : ‚àÆ Œ∂ in C(z, r), (Œ∂ - z)‚Åª¬π ‚Ä¢ f Œ∂ = (2 * œÄ * I : ‚ÑÇ) ‚Ä¢ f z :=
      hd.circle_integral_sub_inv_smul (mem_ball_self hr),
    simp [A, norm_smul, real.pi_pos.le] },
  suffices : ‚à•‚àÆ Œ∂ in C(z, r), (Œ∂ - z)‚Åª¬π ‚Ä¢ f Œ∂‚à• < 2 * œÄ * r * (‚à•f z‚à• / r),
    by rwa [mul_assoc, mul_div_cancel' _ hr.ne'] at this,
  /- This inequality is true because `‚à•(Œ∂ - z)‚Åª¬π ‚Ä¢ f Œ∂‚à• ‚â§ ‚à•f z‚à• / r` for all `Œ∂` on the circle and
  this inequality is strict at `Œ∂ = w`. -/
  have hsub : sphere z r ‚äÜ closed_ball z r, from sphere_subset_closed_ball,
  refine circle_integral.norm_integral_lt_of_norm_le_const_of_lt hr _ _ ‚ü®w, rfl, _‚ü©,
  show continuous_on (Œª (Œ∂ : ‚ÑÇ), (Œ∂ - z)‚Åª¬π ‚Ä¢ f Œ∂) (sphere z r),
  { refine ((continuous_on_id.sub continuous_on_const).inv‚ÇÄ _).smul (hd.continuous_on.mono hsub),
    exact Œª Œ∂ hŒ∂, sub_ne_zero.2 (ne_of_mem_sphere hŒ∂ hr.ne') },
  show ‚àÄ Œ∂ ‚àà sphere z r, ‚à•(Œ∂ - z)‚Åª¬π ‚Ä¢ f Œ∂‚à• ‚â§ ‚à•f z‚à• / r,
  { rintros Œ∂ (hŒ∂ : abs (Œ∂ - z) = r),
    rw [le_div_iff hr, norm_smul, norm_inv, norm_eq_abs, hŒ∂, mul_comm, mul_inv_cancel_left‚ÇÄ hr.ne'],
    exact hz (hsub hŒ∂) },
  show ‚à•(w - z)‚Åª¬π ‚Ä¢ f w‚à• < ‚à•f z‚à• / r,
  { rw [norm_smul, norm_inv, norm_eq_abs, ‚Üê div_eq_inv_mul],
    exact (div_lt_div_right hr).2 hw_lt }
end

/-!
Now we drop the assumption `complete_space F` by embedding `F` into its completion.
-/

lemma norm_max_aux‚ÇÇ {f : ‚ÑÇ ‚Üí F} {z w : ‚ÑÇ} (hd : diff_on_int_cont ‚ÑÇ f (closed_ball z (dist w z)))
  (hz : is_max_on (norm ‚àò f) (closed_ball z (dist w z)) z) :
  ‚à•f w‚à• = ‚à•f z‚à• :=
begin
  set e : F ‚ÜíL[‚ÑÇ] FÃÇ := uniform_space.completion.to_complL,
  have he : ‚àÄ x, ‚à•e x‚à• = ‚à•x‚à•, from uniform_space.completion.norm_coe,
  replace hz : is_max_on (norm ‚àò (e ‚àò f)) (closed_ball z (dist w z)) z,
    by simpa only [is_max_on, (‚àò), he] using hz,
  simpa only [he] using norm_max_aux‚ÇÅ (e.differentiable.comp_diff_on_int_cont hd) hz
end

/-!
Then we replace the assumption `is_max_on (norm ‚àò f) (closed_ball z r) z` with a seemingly weaker
assumption `is_max_on (norm ‚àò f) (ball z r) z`.
-/

lemma norm_max_aux‚ÇÉ {f : ‚ÑÇ ‚Üí F} {z w : ‚ÑÇ} {r : ‚Ñù} (hr : dist w z = r)
  (hd : diff_on_int_cont ‚ÑÇ f (closed_ball z r)) (hz : is_max_on (norm ‚àò f) (ball z r) z) :
  ‚à•f w‚à• = ‚à•f z‚à• :=
begin
  subst r,
  rcases eq_or_ne w z with rfl|hne, { refl },
  have : closure (ball z (dist w z)) = closed_ball z (dist w z),
    from closure_ball z (dist_ne_zero.2 hne),
  exact norm_max_aux‚ÇÇ hd (this ‚ñ∏ hz.closure (this.symm ‚ñ∏ hd.continuous_on.norm))
end

/-!
Finally, we generalize the theorem from a disk in `‚ÑÇ` to a closed ball in any normed space.
-/

/-- **Maximum modulus principle** on a closed ball: if `f : E ‚Üí F` is continuous on a closed ball,
is complex differentiable on the corresponding open ball, and the norm `‚à•f w‚à•` takes its maximum
value on the open ball at its center, then the norm `‚à•f w‚à•` is constant on the closed ball.  -/
lemma norm_eq_on_closed_ball_of_is_max_on {f : E ‚Üí F} {z : E} {r : ‚Ñù}
  (hd : diff_on_int_cont ‚ÑÇ f (closed_ball z r)) (hz : is_max_on (norm ‚àò f) (ball z r) z) :
  eq_on (norm ‚àò f) (const E ‚à•f z‚à•) (closed_ball z r) :=
begin
  intros w hw,
  rw [mem_closed_ball, dist_comm] at hw,
  rcases eq_or_ne z w with rfl|hne, { refl },
  set e : ‚ÑÇ ‚Üí E := line_map z w,
  have hde : differentiable ‚ÑÇ e := (differentiable_id.smul_const (w - z)).add_const z,
  suffices : ‚à•(f ‚àò e) (1 : ‚ÑÇ)‚à• = ‚à•(f ‚àò e) (0 : ‚ÑÇ)‚à•, by simpa [e],
  have hr : dist (1 : ‚ÑÇ) 0 = 1, by simp,
  have hball : maps_to e (ball 0 1) (ball z r),
  { refine ((lipschitz_with_line_map z w).maps_to_ball
      (mt nndist_eq_zero.1 hne) 0 1).mono subset.rfl _,
    simpa only [line_map_apply_zero, mul_one, coe_nndist] using ball_subset_ball hw },
  refine norm_max_aux‚ÇÉ hr (diff_on_int_cont.mk_ball
    (hd.differentiable_on_ball.comp hde.differentiable_on hball)
    (hd.continuous_on.comp hde.continuous.continuous_on _)) _,
  { refine ((lipschitz_with_line_map z w).maps_to_closed_ball 0 1).mono_right _,
    simpa only [line_map_apply_zero, mul_one, coe_nndist] using closed_ball_subset_closed_ball hw },
  { exact hz.comp_maps_to hball (line_map_apply_zero z w) }
end

/-!
### Different forms of the maximum modulus principle
-/

/-- **Maximum modulus principle**: if `f : E ‚Üí F` is complex differentiable on a set `s`, the norm
of `f` takes it maximum on `s` at `z` and `w` is a point such that the closed ball with center `z`
and radius `dist w z` is included in `s`, then `‚à•f w‚à• = ‚à•f z‚à•`. -/
lemma norm_eq_norm_of_is_max_on_of_closed_ball_subset {f : E ‚Üí F} {s : set E} {z w : E}
  (hd : diff_on_int_cont ‚ÑÇ f s) (hz : is_max_on (norm ‚àò f) s z)
  (hsub : closed_ball z (dist w z) ‚äÜ s) :
  ‚à•f w‚à• = ‚à•f z‚à• :=
norm_eq_on_closed_ball_of_is_max_on (hd.mono hsub)
  (hz.on_subset $ ball_subset_closed_ball.trans hsub) (mem_closed_ball.2 le_rfl)

/-- **Maximum modulus principle**: if `f : E ‚Üí F` is complex differentiable in a neighborhood of `c`
and the norm `‚à•f z‚à•` has a local maximum at `c`, then `‚à•f z‚à•` is locally constant in a neighborhood
of `c`. -/
lemma norm_eventually_eq_of_is_local_max {f : E ‚Üí F} {c : E}
  (hd : ‚àÄ·∂† z in ùìù c, differentiable_at ‚ÑÇ f z) (hc : is_local_max (norm ‚àò f) c) :
  ‚àÄ·∂† y in ùìù c, ‚à•f y‚à• = ‚à•f c‚à• :=
begin
  rcases nhds_basis_closed_ball.eventually_iff.1 (hd.and hc) with ‚ü®r, hr‚ÇÄ, hr‚ü©,
  exact nhds_basis_closed_ball.eventually_iff.2 ‚ü®r, hr‚ÇÄ, norm_eq_on_closed_ball_of_is_max_on
    (differentiable_on.diff_on_int_cont $ Œª x hx, (hr hx).1.differentiable_within_at) $
    Œª x hx, (hr $ ball_subset_closed_ball hx).2‚ü©
end

lemma is_open_set_of_mem_nhds_and_is_max_on_norm {f : E ‚Üí F} {s : set E}
  (hd : differentiable_on ‚ÑÇ f s) :
  is_open {z | s ‚àà ùìù z ‚àß is_max_on (norm ‚àò f) s z} :=
begin
  refine is_open_iff_mem_nhds.2 (Œª z hz, (eventually_eventually_nhds.2 hz.1).and _),
  replace hd : ‚àÄ·∂† w in ùìù z, differentiable_at ‚ÑÇ f w, from hd.eventually_differentiable_at hz.1,
  exact (norm_eventually_eq_of_is_local_max hd $ (hz.2.is_local_max hz.1)).mono
    (Œª x hx y hy, le_trans (hz.2 hy) hx.ge)
end

/-- **Maximum modulus principle**: if `f : E ‚Üí F` is complex differentiable on a nonempty compact
set `K`, then there exists a point `z ‚àà frontier K` such that `Œª z, ‚à•f z‚à•` takes it maximum value on
`K` at `z`. -/
lemma exists_mem_frontier_is_max_on_norm [nontrivial E] {f : E ‚Üí F} {K : set E} (hK : is_compact K)
  (hne : K.nonempty) (hd : diff_on_int_cont ‚ÑÇ f K) :
  ‚àÉ z ‚àà frontier K, is_max_on (norm ‚àò f) K z :=
begin
  rcases hK.exists_forall_ge hne hd.continuous_on.norm with ‚ü®w, hwK, hle‚ü©,
  rcases hK.exists_mem_frontier_inf_dist_compl_eq_dist hwK with ‚ü®z, hzK, hzw‚ü©,
  refine ‚ü®z, hzK, Œª x hx, (hle x hx).trans_eq _‚ü©,
  refine (norm_eq_norm_of_is_max_on_of_closed_ball_subset hd hle _).symm,
  calc closed_ball w (dist z w) = closed_ball w (inf_dist w K·∂ú) : by rw [hzw, dist_comm]
  ... ‚äÜ closure K : closed_ball_inf_dist_compl_subset_closure hwK
  ... = K : hK.is_closed.closure_eq
end

/-- **Maximum modulus principle**: if `f : E ‚Üí F` is complex differentiable on a compact set `K` and
`‚à•f z‚à• ‚â§ C` for any `z ‚àà frontier K`, then the same is true for any `z ‚àà K`. -/
lemma norm_le_of_forall_mem_frontier_norm_le [nontrivial E] {f : E ‚Üí F} {K : set E}
  (hK : is_compact K) (hd : diff_on_int_cont ‚ÑÇ f K)
  {C : ‚Ñù} (hC : ‚àÄ z ‚àà frontier K, ‚à•f z‚à• ‚â§ C) {z : E} (hz : z ‚àà K) :
  ‚à•f z‚à• ‚â§ C :=
let ‚ü®w, hwK, hw‚ü© := exists_mem_frontier_is_max_on_norm hK ‚ü®z, hz‚ü© hd
in le_trans (hw hz) (hC w hwK)

/-- If two complex differentiable functions `f g : E ‚Üí F` are equal on the boundary of a compact set
`K`, then they are equal on `K`. -/
lemma eq_on_of_eq_on_frontier [nontrivial E] {f g : E ‚Üí F} {K : set E} (hK : is_compact K)
  (hf : diff_on_int_cont ‚ÑÇ f K) (hg : diff_on_int_cont ‚ÑÇ g K) (hfg : eq_on f g (frontier K)) :
  eq_on f g K :=
begin
  suffices H : ‚àÄ z ‚àà K, ‚à•f z - g z‚à• ‚â§ 0, by simpa [sub_eq_zero] using H,
  convert Œª z hz, norm_le_of_forall_mem_frontier_norm_le hK (hf.sub hg) _ hz,
  simpa [sub_eq_zero]
end

end complex
